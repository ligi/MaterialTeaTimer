apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.trevjonez.composer'
apply plugin: 'org.jetbrains.kotlin.android.extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.ligi.materialteatimer"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 14
        versionName "1.4"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "MaterialTeaTimer-$versionName"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "fire", "store"

    productFlavors {

        withFirebase {
            dimension "fire"
        }

        noFirebase {
            dimension "fire"
        }

        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }
    }

    android.variantFilter { variant ->
        def fireBase = variant.getFlavors().get(0).name
        def store = variant.getFlavors().get(1).name

        variant.setIgnore((project.hasProperty("singleFlavor") && (store != 'forPlay') && (build != 'prod')) ||
                ((store == 'forAmazon' || store == 'forPlay') && fireBase == 'noFirebase') ||
                (store == 'forFDroid' && fireBase != 'noFirebase'))

    }


    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        warning 'MissingTranslation'
    }
}

dependencies {

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'

    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"

    implementation 'com.github.ligi:ExtraCompats:1.0'
    implementation 'com.github.ligi:KAXT:1.0'

    withFirebaseImplementation 'com.google.firebase:firebase-core:11.8.0'

    androidTestImplementation 'androidx.appcompat:appcompat:1.0.2'
    androidTestImplementation 'com.google.android.material:material:1.0.0'

    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'com.github.ligi:trulesk:0.28'
}

if (hasProperty("gms")) {
    apply plugin: 'com.google.gms.google-services'
}

android.applicationVariants.all { variant ->
    if (!variant.name.contains("oFirebase")) {
        project.tasks.each { t ->
            if (t.name.contains("GoogleServices")) {
                // Remove google services plugin
                variant.getVariantData().resourceGenTask.getTaskDependencies().values.remove(t);
            }
        }
    }
}